
cmake_minimum_required(VERSION 3.16)

##################################################################################################################
# Setup
##################################################################################################################
project(QuickCutConsole)

add_definitions(
    -DUNICODE
    -D_UNICODE
    -DBOOST_BIND_GLOBAL_PLACEHOLDERS
)

include_directories(
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${QUICKCUT_SHARED_DIR}
)

##################################################################################################################
# Project Files
##################################################################################################################
set(MANAGERS_CXX_FILES
    ${QUICKCUT_MANAGERS_DIR}/IParserOperations.h
    ${QUICKCUT_MANAGERS_DIR}/BaseManager.h
    ${QUICKCUT_MANAGERS_DIR}/BaseManager.cpp
    ${QUICKCUT_MANAGERS_DIR}/ProfileManager.h
    ${QUICKCUT_MANAGERS_DIR}/ProfileManager.cpp
    ${QUICKCUT_MANAGERS_DIR}/ActionManager.h
    ${QUICKCUT_MANAGERS_DIR}/ActionManager.cpp
)
set(PARSER_CXX_FILES
    ${QUICKCUT_PARSER_DIR}/BaseParser.h
    ${QUICKCUT_PARSER_DIR}/ProfileParser.h
    ${QUICKCUT_PARSER_DIR}/ProfileParser.cpp
)
set(MODELS_CXX_FILES
    ${QUICKCUT_MODELS_DIR}/BaseModel.h
    ${QUICKCUT_MODELS_DIR}/BaseModel.cpp
    ${QUICKCUT_MODELS_DIR}/Profile.h
    ${QUICKCUT_MODELS_DIR}/Profile.cpp
    ${QUICKCUT_MODELS_DIR}/Action.h
    ${QUICKCUT_MODELS_DIR}/Action.cpp
)
set(SHARED_CXX_FILES
    ${QUICKCUT_SHARED_DIR}/pch.h
    ${QUICKCUT_SHARED_DIR}/pch.cpp
    ${QTSINGLEAPP_CXX_FILES}
)
set(MISC_CXX_FILES
    main.cpp
    QuickCutConsole.h
    QuickCutConsole.cpp
)

if(WIN32)
    set(MISC_CXX_FILES
        ${MISC_CXX_FILES}
        QuickCutConsoleWindows.h
        QuickCutConsoleWindows.cpp
    )
    set(RESOURCE_FILES
        Resources/QuickCutConsole.rc
    )
elseif(UNIX)
    set(MISC_CXX_FILES
        ${MISC_CXX_FILES}
        QuickCutConsoleUnix.h
        QuickCutConsoleUnix.cpp
    )
    set(RESOURCE_FILES
        "" # TODO(Gilad): Create resource for unix systems.
    )
endif()

set(CXX_FILES
    ${MISC_CXX_FILES}
    ${SHARED_CXX_FILES}
    ${MANAGERS_CXX_FILES}
    ${MODELS_CXX_FILES}
    ${PARSER_CXX_FILES}
    ${QUICKCUT_UTILS_CXX_FILES}
)

##################################################################################################################
# IDE Representation
##################################################################################################################
source_group("Source Files" FILES
    ${MISC_CXX_FILES}
)
source_group("Source Files\\Shared" FILES
    ${SHARED_CXX_FILES}
)
source_group("Source Files\\Shared\\Managers" FILES
    ${MANAGERS_CXX_FILES}
)
source_group("Source Files\\Shared\\Models" FILES
    ${MODELS_CXX_FILES}
)
source_group("Source Files\\Shared\\Parser" FILES
    ${PARSER_CXX_FILES}
)
source_group("Source Files\\Shared\\Utils" FILES
    ${QUICKCUT_UTILS_CXX_FILES}
)
source_group("Resources" FILES
    ${RESOURCE_FILES}
)

##################################################################################################################
# Qt Framework
##################################################################################################################
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTORCC ON)
#qt5_wrap_ui()
#qt5_wrap_cpp()


##################################################################################################################
# Target
##################################################################################################################
add_executable(${PROJECT_NAME}
    ${CXX_FILES}
    ${RESOURCE_FILES}
)

if(WIN32)
    target_link_libraries(${PROJECT_NAME} PUBLIC
        Qt5::WinMain
        Qt5::Core
    )
else() # UNIX
    target_link_libraries(${PROJECT_NAME} PUBLIC
        Qt5::Core
    )
endif()

target_precompile_headers(${PROJECT_NAME} PRIVATE ${QUICKCUT_SHARED_DIR}/pch.h)
# target_precompile_headers(${PROJECT_NAME} REUSE_FROM QuickCut) # slow and some warnings.

if(WIN32)
    set_property(TARGET ${PROJECT_NAME} APPEND_STRING PROPERTY LINK_FLAGS " /MANIFESTUAC:\"level='requireAdministrator'\"")
endif()
